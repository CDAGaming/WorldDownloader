buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'commons-io:commons-io:2.5'
        classpath 'com.googlecode.java-diff-utils:diffutils:1.3.0'
        classpath 'pokechu22.test.begradle:begradle:0.1.3-SNAPSHOT@jar'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

configurations {
    shade
    compile.extendsFrom(shade)
}

apply plugin: 'java'

dependencies {
    shade 'it.unimi.dsi:fastutil:7.0.12_mojang'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.13.0'
}

version = "4.0.1.8-SNAPSHOT"
group = "wdl"
archivesBaseName = "LiteModWDL"
libsDirName = "../../libs/${version}"

apply plugin: 'net.minecraftforge.gradle.liteloader'
apply plugin: 'pokechu22.test.customsrg'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'pokechu22.test.langrenamer'
apply plugin: 'pokechu22.test.noinplacereobf'

minecraft {
    // 1.9 actually does work with FG 2.2, but it'll claim it doesn't
    suppressVersionTest = true
    version = "1.9"
    mappings = "snapshot_20180119"
    runDir = "run"

    // We don't currently want a source jar (doesn't work well for base edits, and the version info doesn't work yet)
    makeObfSourceJar = false
    // en_us.lang to en_US.lang
    ext.renameLangFiles = true
}

extraSrgs {
    addSrg file('../srgs/EntityHorseToEquineEntity.srg')
    addSrg file('../srgs/AnimalChestToContainerHorseChest.srg')
    addSrg file('../srgs/HorseArmorTypeSwap.srg')
    addSrg file('../srgs/ChunkCoordIntPairToChunkPos.srg')
    addMethods file('../srgs/liteloader_1.9_methods.csv')
    addMethods file('../srgs/1.9.2_methods.csv')
    addMethods file('../srgs/old_chest_methods.csv')
    addFields file('../srgs/1.9.2_fields.csv')
    addPatch('net.minecraft.entity.passive.EquineEntity.java', file('../srgs/EE_1.9_1.9.2.patch'))
    addPatch('net.minecraft.server.management.PlayerManager.java', file('../srgs/PlayerManager_1.9.patch'))
}

sourceSets {
    main {
        java {
            srcDir project(':1.9').file("src/main/java")
            srcDir project(':share').file("src/main/java")
            exclude "wdl/handler/block/package-info.java"
            exclude "wdl/handler/block/ShulkerBoxHandler.java"
        }
        resources {
            srcDir "src/main/resources"
            srcDir project(':share').file("src/main/resources")
        }
    }
    mixin {
        java {
            srcDir "src/java6/java"
            compileClasspath += sourceSets.main.output
            compileClasspath += sourceSets.main.compileClasspath
            runtimeClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.runtimeClasspath
        }
        // Refmap declaration must match the refmap name specified in the json config
        ext.refMap = "mixins.LiteModWDL.refmap.json"
    }
    test {
        java {
            srcDir project(':1.9').file("src/test/java")
            srcDir project(':share').file("src/test/java")
        }
    }
    api {
        java {
            srcDir project(':share').file("src/api/java")
        }
    }
}

// We want Java 8 where possible
tasks.withType(JavaCompile) { javaTask ->
    javaTask.sourceCompatibility = "1.8"
    javaTask.targetCompatibility = "1.8"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

// Mixins need to be in Java 6 for this version of MC
compileMixinJava {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"
}

mixin {
    defaultObfuscationEnv notch
}

litemod {
    json {
        name = "LiteModWDL"
        displayName = "World Downloader for Liteloader"
        author = "uyjulian, pokechu22"
        mcversion = "1.9"
        mixinConfigs += "mixins.LiteModWDL.json"
        version = project.version
    }
}

// Use tabs instead of spaces
fixMcSources {
    astyleConfig = "../astyle.cfg"
}

// Ensure that the API code, Java 6 mixin code, and litemod JSON are in the jar
jar {
    from sourceSets.mixin.output
    from sourceSets.api.output
    from litemod.outputs
    configurations.shade.each {
        from(zipTree(it)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}
